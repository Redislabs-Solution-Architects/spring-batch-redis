plugins {
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

group = 'com.redislabs'
description = 'Spring Batch Redis'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    api 'io.lettuce:lettuce-core:6.1.1.RELEASE'
    api 'org.apache.commons:commons-pool2'
    api ('org.springframework.boot:spring-boot-starter-batch') {
        exclude group: 'io.lettuce', module: 'lettuce-core'
    }
    testImplementation 'io.micrometer:micrometer-core'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.hsqldb:hsqldb'
    testImplementation 'com.redislabs:testcontainers-redis:1.1.2'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Spring Batch Redis'
                description = 'Spring Batch reader and writer implementations for Redis'
                url = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redis'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Redislabs-Solution-Architects/spring-batch-redis'
                    developerConnection = 'scm:git:git@github.com:Redislabs-Solution-Architects/spring-batch-redis'
                    url = 'https://github.com/Redislabs-Solution-Architects/spring-batch-redis'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "Redislabs-Solution-Architects"
    repo "spring-batch-redis"
    body changelog()
}

def isNonStable = { String version ->
    def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+([.-]r)?$/
    return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {

    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }

}

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn ":publishToSonatype"
afterReleaseBuild.dependsOn ":closeAndReleaseSonatypeStagingRepository"